//
// Server.idl
//
// This file will be processed by the MIDL compiler to
// produce the type library (server.tlb) and marshaling code.
//

import "oaidl.idl" ;

typedef [uuid(00FABB0F-5691-41A6-B7C1-11606671F8E5)]
struct StructRecordParamTest {
	BSTR question ;
	long answer ;
	VARIANT_BOOL needs_clarification ;
} StructRecordParamTest ;


// Interface IDualRecordParamTest
[
	odl,
	uuid(0C4E01E8-4625-46A2-BC4C-2E889A8DBBD6),
	dual,
	helpstring("Dual Interface for testing record parameters."),
	nonextensible,
	oleautomation
]
interface IDualRecordParamTest : IDispatch
{
	[id(0x00000001)]
	HRESULT InitRecord([in, out] StructRecordParamTest* test_record) ;
	[id(0x00000002)]
	HRESULT VerifyRecord(
					[in] StructRecordParamTest* test_record,
					[out, retval] VARIANT_BOOL* result);
} ;


// Interface IDispRecordParamTest
[
	uuid(033E4C10-0A7F-4E93-8377-499AD4B6583A),
	helpstring("Dispinterface for testing record parameters.")
]
dispinterface IDispRecordParamTest
{
	interface IDualRecordParamTest;
} ;


// Interface IDualSafearrayParamTest
[
	odl,
	uuid(1F4F3B8B-D07E-4BB6-8D2C-D79B375696DA),
	dual,
	helpstring("IDualSafearrayParamTest Interface"),
	nonextensible,
	oleautomation
]
interface IDualSafearrayParamTest : IDispatch
{
	[id(0x00000001)]
	HRESULT InitArray([in, out] SAFEARRAY(double)* test_array) ;
	[id(0x00000002)]
	HRESULT VerifyArray(
					[in] SAFEARRAY(double) test_array,
					[out, retval] VARIANT_BOOL* result);
} ;


// Interface IDispSafearrayParamTest
[
	uuid(4097A6D0-A111-40E2-BD0B-177B775A9496)
]
dispinterface IDispSafearrayParamTest
{
	interface IDualSafearrayParamTest;
} ;


//
// Component and type library descriptions
//
[
	uuid(07D2AEE5-1DF8-4D2C-953A-554ADFD25F99),
	version(1.0),
	helpstring("Comtypes C++ Test COM Server 1.0 Type Library.")
]
library ComtypesCppTestSrvLib
{
	// TLib : OLE Automation : {00020430-0000-0000-C000-000000000046}
	importlib("stdole2.tlb") ;

	// CoComtypesDispRecordParamTest
	// Component that implements interfaces used for dispinterface record parameter tests.
	[
		uuid(5E78C9A8-4C19-4285-BCD6-3FFBBA5B17A8),
		helpstring("Comtypes component for dispinterface record parameter tests.")
	]
	coclass CoComtypesDispRecordParamTest
	{
		interface IDualRecordParamTest ;
		dispinterface IDispRecordParamTest ;
	} ;


	// CoComtypesDispSafearrayParamTest
	// Component that implements interfaces used for dispinterface Safearray parameter tests.
	[
		uuid(091D762E-FF4B-4532-8B24-23807FE873C3),
		helpstring("Comtypes component for dispinterface Safearray parameter tests.")
	]
	coclass CoComtypesDispSafearrayParamTest
	{
		interface IDualSafearrayParamTest ;
		dispinterface IDispSafearrayParamTest ;
	} ;
} ;
